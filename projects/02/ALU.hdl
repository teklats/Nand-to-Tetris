// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    /// x after zx 
    Mux16 (a = x, b = false, sel = zx, out = xZX);

    /// x after nx
    Not16 (in = xZX, out = notxZX);
    Mux16 (a = xZX, b = notxZX, sel = nx, out = newX); 

    /// y after zy
    Mux16 (a = y, b = false, sel = zy, out = yZY);

    /// y after ny
    Not16 (in = yZY, out = notYZY);
    Mux16 (a = yZY, b = notYZY, sel = ny, out = newY); 


    Add16 (a = newX, b = newY, out = XsumY);
    And16 (a = newX, b = newY, out = XandY);

    /// x+y or x&y depending on f
    Mux16 (a = XandY, b = XsumY, sel = f, out = answer);

    Not16 (in = answer, out = notAnswer);

    ///final output
    Mux16 (a = answer, b = notAnswer, sel = no, out = out, out[0..7] = half1, out[8..15] = half2, out[15] = ng);

    Or8Way (in = half1, out = halfAnswer1);
    Or8Way (in = half2, out = halfAnswer2);

    Or (a = halfAnswer1, b = halfAnswer2, out = zeroOut);
    Mux (a = true, b = false, sel = zeroOut, out = zr);

}